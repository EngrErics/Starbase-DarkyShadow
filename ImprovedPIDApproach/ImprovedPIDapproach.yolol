s=17 m=500 ib=5 t=20 p=0 i=0 x=ib+s u=t g=0.001
if :Ap*(:M<m) then goto3+:insideSafeZone end goto1
Kp=0.056 Ki=0.001 Kd=5.4 h=0.50 l=1-h p=:M-s goto5 //NSZ Conservative
Kp=0.080 Ki=0.001 Kd=5.8 h=0.50 l=1-h p=:M-s goto5 //SZ  Aggressive
e=:M-s d=(p-e)*Kd*h+d*l i+=e*Ki*(:M<x) o=Kp*e+i f=1-t/u t-=1*(t>0) p=e
z/=:Ap*(:M<m)*(d*d>g+e*e>1) :fcuforward=o*f :fcubackward=(d-i)*f goto5
if (d*d>g+e*e>1)==0 then :Ap=0 end :fcuforward=0 :fcubackward=0 goto1

//Improved PID - Approximated Profiled Motion PID
// by: Darkyshadow built and tuned for Ghidorah of HLA
// tips by in game mail are not necessary but appreciated

//Tested Tunings
//Kp=0.056 Ki=0.001 Kd=5.4 h=0.50 //Super Conservative
//Kp=0.080 Ki=0.001 Kd=5.8 h=0.50 //Conservative Bulletproof
//Kp=0.090 Ki=0.001 Kd=5.8 h=0.80 //Slightly More Aggressive
